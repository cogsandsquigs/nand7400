/// An entire source code file, which is a sequence of labels and instructions. This contains `NEWLINE*` because this
/// assembly has significant newlines, but sometimes they are not needed or wanted. NEWLINe*` Because of this, newlines
/// cannot be counted as regular whitespace, so they have to be explicitly included manually handled in the grammar.
/// All we do here is mop up the newlines at the beginning and end of the file, and then parse everything in between.
/// Its ok that we dont get rid of extra newlines in the middle, because an EOL can be many newlines in a row,
/// technically.
File = { SOI ~ NEWLINE* ~ (Label | Instruction)* ~ NEWLINE* ~ EOI }

/// A label, which is an identifier followed by a colon.
Label = { Identifier ~ Colon ~ EOL }

/// A single instruction, which is an identifier followed by a sequence of identifiers and literals.
Instruction = { Identifier ~ (Identifier | Literal)* ~ EOL }

/// An identifier value, which is a sequence of letters, numbers, and underscores.
Identifier = @{ (LETTER | "_") ~ (LETTER | LETTER_NUMBER | ASCII_ALPHANUMERIC | "_")* }

/// A literal value, in decimal, hex, binary, or octal.
Literal = @{
    HexLiteral
  | BinaryLiteral
  | OctalLiteral
  | DecimalLiteral
}

/// A hexidecimal literal.
HexLiteral = @{ ("0x" | "0X") ~ ASCII_HEX_DIGIT+ }

/// A binary literal.
BinaryLiteral = @{ ("0b" | "0B") ~ ASCII_BIN_DIGIT+ }

/// An octal literal.
OctalLiteral = @{ ("0o" | "0O") ~ ASCII_OCT_DIGIT+ }

/// A decimal literal.
DecimalLiteral = @{ ASCII_DIGIT+ }

/// A colon character. This is significant because it delimits labels.
Colon = @{ ":" }

/// The end of a line, which is a newline character or EOI.
EOL = @{ NEWLINE+ | &EOI }

/// A single whitespace character. This does NOT include newlines, because nelwines have special significance in this
/// assembly language.
WHITESPACE = _{ " " | "\t" }

/// A comment, either single- or multi-line.
COMMENT = _{
    ("//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE) // A single-line comment starts with "//" and ends with a newline.

  | ("/*" ~ (!"*/" ~ ANY)* ~ "*/") // A multi-line comment starts with "/*" and ends with "*/".
}
