/// An entire source code file, which is a sequence of labels and instructions.
File = { SOI ~ (Label | Instruction)* ~ EOI }

/// A label, which is an identifier followed by a colon.
Label = { Identifier ~ ":" ~ (NEWLINE+ | &EOI) }

/// A single instruction, which is an identifier followed by a sequence of identifiers and literals.
Instruction = { Identifier ~ (Identifier | Literal)* ~ (NEWLINE+ | &EOI) }

/// An identifier value, which is a sequence of letters, numbers, and underscores.
Identifier = @{ (LETTER | "_") ~ (LETTER | LETTER_NUMBER | ASCII_ALPHANUMERIC | "_")* }

/// A literal value, in decimal, hex, binary, or octal.
Literal = @{
    HexLiteral
  | BinaryLiteral
  | OctalLiteral
  | DecimalLiteral
}

/// A hexidecimal literal.
HexLiteral = @{ ("0x" | "0X") ~ ASCII_HEX_DIGIT+ }

/// A binary literal.
BinaryLiteral = @{ ("0b" | "0B") ~ ASCII_BIN_DIGIT+ }

/// An octal literal.
OctalLiteral = @{ ("0o" | "0O") ~ ASCII_OCT_DIGIT+ }

/// A decimal literal.
DecimalLiteral = @{ ASCII_DIGIT+ }

/// A single whitespace character. This does NOT include newlines.
WHITESPACE = _{ " " | "\t" }

/// A comment, either single- or multi-line.
COMMENT = _{
    ("//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE) // A single-line comment starts with "//" and ends with a newline.

  | ("/*" ~ (!"*/" ~ ANY)* ~ "*/") // A multi-line comment starts with "/*" and ends with "*/".
}
